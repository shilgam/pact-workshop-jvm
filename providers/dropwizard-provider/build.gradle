apply plugin: 'application'

mainClassName = 'au.com.dius.pactworkshop.dropwizardprovider.MainApplication'

dependencies {
  compile 'io.dropwizard:dropwizard-core:1.3.1'

  testCompile 'junit:junit:4.12'
  testCompile 'io.dropwizard:dropwizard-testing:1.3.1'
  testCompile 'au.com.dius.pact.provider:junit:4.1.2'
}

jar {
  manifest {
    attributes("Class-Path": (configurations.compile + configurations.runtime).collect { it.getName() }.join(' ') ,
      "Main-Class": mainClassName
    )
  }
}

run {
  if ( project.hasProperty("appArgs") ) {
    args appArgs.split(',')
  } else {
    args 'server'
  }
}

test {
  systemProperty 'pactBrokerHost', pactBrokerHost
  def pactBrokerToken = (project.pactBrokerToken == '<YOUR TOKEN>') ? System.getenv('PACT_BROKER_TOKEN') : project.pactBrokerToken
  systemProperty 'pactBrokerToken', pactBrokerToken
  systemProperty 'pact.provider.version', "dropwiz-" + System.getenv('TRAVIS_COMMIT')
  systemProperty 'pact.provider.tag', System.getenv('TRAVIS_BRANCH')

  systemProperty 'pact.verifier.publishResults', project['pact.verifier.publishResults']
}
